#!ipxe
#
# This is the iPXE boot script that we embed into the iPXE binary.
#
# The default behaviour is to get DHCP and assume that DHCP includes
# the filename option. If it fails, then it just stops.
#
# This script implements more retries until both DHCP and the filename
# option are present. This makes sure that the underlying network has
# plenty of time to be ready. If this still fails, then rather than
# halt, then host will reboot to try again.
#
# Based on:
# https://github.com/danderson/netboot/blob/master/pixiecore/boot.ipxe
#

prompt --key 0x02 --timeout 2000 Press Ctrl-B for the iPXE command line... && shell ||

set attempts:int32 10
set x:int32 1

:dhcploop
dhcp || goto nodhcp
isset ${filename} || goto nobootconfig
goto boot

:nodhcp
echo -n No DHCP response
goto retry

:nobootconfig
echo -n No filename option present
goto retry

:retry
echo (attempt ${x}/${attempts}).
iseq ${x} ${attempts} && goto fail ||
inc x
sleep 1
goto dhcploop

:boot
echo Booting using ${ifname}.

# This is a hack to workaround LP bug 1845487:
#   https://bugs.launchpad.net/puppet-ironic/+bug/1845487
# It should be removed when the fix for that bug is available and backported to
# all supported releases.
isset ${mtu} && echo -n Overriding MTU from dhcp of ${mtu}.
set mtu 1350
echo Set MTU to ${mtu}.

chain ${filename}

:fail
echo Failed to get a correct DHCP response after ${attempts} attempts.
echo
echo Rebooting in 5 seconds...
sleep 5
reboot
